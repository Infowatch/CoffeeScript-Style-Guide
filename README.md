# CoffeeScript Style Guide

#### Кодировка
Все стили продукта должны использовать кодировку UTF-8.

#### Ширина строки
Разрешенная максимальная длина строки в файлах равно 80 символам. Делая строчки не большими вы облегчаете ревью кода, а также сравнение двух файлов в IDE. 

#### Пустые строки
  - Необходимо оставлять последнюю строчку файла пустой, лучше всего настроить IDE 
  - Необходимо вставлять пустую строку между определениями функций или методов класса
  - Необходимо вставлять две пустые строки между разными определениями классов
  - Необходимо отделять пустой строкой логические участки кода внутри функций и методов
  - Необходимо отделять пустой строкой методы, а также функции при описании свойств объекта (см. Объекты)

#### Использование strict режима
Каждый файл исходного кода должен начинаться с "use strict"

#### Исключения (exceptions)
  - Запрещается подавлять исключения в коде.
  - Если исключение было поймано, оно обязательно должно быть обработано

```coffeescript
# bad --------------------------
try
  null.toString()
  
# acceptable -------------------
try
  null.toString()
catch e
  sendErrorToServer e.message, e.stack
  throw e
```

#### Парадигма программирования
  - Код должен быть написан в императивном стиле
  - Запрещено смешивать парадигмы в рамках одного продукта

```coffeescript
# good -------------------------
tasks = common_tasks.concat [
  "concat:locale_RU"
  "concat:locale_EN"
  "sass:dist"
]
.concat [
  if target is "watch"
     "watch"
   else
     []
]
 
grunt.task.run tasks

# bad --------------------------
grunt.task.run if 1
  common_tasks
  .concat [
     "concat:locale_RU"
     "concat:locale_EN"
     "sass:dist"
  ]
  .concat if target is "watch"
    "watch"
  else
    []
```

#### Обработка данных
  - Следует использовать библиотеки для обработки данных, такие как lodash, и расширять их возможности
  - Категорически запрещено вносить изменения в нативные объекты языка Javascript

```coffeescript
# good -------------------------
_.mixin capitalize: (string) -> # ...

_.capitalize String(obj)

isArray = $.type null # null

# bad --------------------------
String.prototype.capitalize = -> # ...

Object::toString.call(obj).capitalize()

isArray = typeof null # object
```

#### Отступы
  - Во всех файлах должен использоваться отступ  в два пробела
  - Для простого поддержания данного правила необходимо настроить используемое IDE
  - При описании элементов объекта (Object) или массива (Array) отступ перед закрывающей скобкой должен соответствовать отступу перед выражением, содержащим открывающую скобку

```coffeescript 
# good -------------------------
foo = [
  'some'
  'string'
  'values'
]

# bad --------------------------
foo = [
    'some'
    'string'
    'values'
    ]
```

  - В блоке отступ перед каждым условием должен совпадать с отступом перед первой строкой блока

```coffeescript 
# good -------------------------
if a is b and
   foo()
  bar()
 
if a is b and
 foo()
  bar()
 
 # bad --------------------------
if a is b and
foo()
  bar()
```

  - При описании цепочки вызовов (call chain), необходимо сохранять отступ

```coffeescript 
# good -------------------------
_(arr).map arr, (el) ->
    return a if el.is("a")
.compact()
.value()

# bad --------------------------
_(arr).map(arr, (el) ->
  return a if el.is("a")
  )
  .compact()
  .value()
```

#### Запятые
  - Избегайте использования запятых до символа новой строки, при описании элементов объекта (Object) или массива (Array), когда элементы перечислены в отдельных строках.

```coffeescript 
# good -------------------------
foo = [
  'some'
  'string'
  'values'
]
bar:
  label : 'test'
  value : 87

# bad --------------------------
foo = [
  'some',
  'string',
  'values'
]
bar:
  label: 'test',
  value: 87

```

#### Пробелы
  - Не используйте дополнительные пробелы:
    - перед скобками и после них
    - перед запятыми
    - в конце строки (trailing spaces)
    
  - Используйте 1 пробел (c учетом вышеописанного):
    - после
      - запятых
      - двоеточия в однострочной форме записи объекта
    - перед и после:
      - операторов присваивания `=`
      - операторов дополнения значений `-=`, `or=`, `+=`, etc
      - операторов сравнения `>=`, `<`, etc
      - арефметических операторов `*`, `+`, `-`, `/`, etc
      - симолвами `->` и `=>`
  

```coffeescript 
# good -------------------------
a = [1, 2, 3, 4]
b = {foo: true}
c = ($ 'body')
  
console.log x, y
 
test: (param = null) ->
  return
  
x +=  1
y or= true

# bad --------------------------
a = [1, 2, 3, 4]
b = {foo: true}
c = ($ 'body')
  
console.log x, y
 
test: (param = null) ->
  return
  
x +=  1
y or= true

```

  - Необходимо выравнивать символы `=` `:` при многострочном описании блока присваивания или объекта в случае если для выравнивания не потребуется больше 12 пробелов. Если такое происходит необходимо либо отделить разбить свойства/переменные на группы
 
```coffeescript 
# good -------------------------
x           = getX()
y           = getY()
coordinates = [x, y]
  
extraLongVariableName = 3

# bad --------------------------
x                     = getX()
y                     = getY()
coordinates           = [x, y]
extraLongVariableName = 3
```
 
  - выравнивать `:` необходимо для каждого объекта по отдельности

```coffeescript 
# good -------------------------
options:
  patterns:
    "/^\[A-Z]/" : "const"
    "/^\_/"     : "private"

# bad --------------------------
options         :
  patterns      :
    "/^\[A-Z]/" : "const"
    "/^\_/"     : "private"
```

  - Если описание объекта содержит функции, то они должны быть описаны в конце и отделены пустыми строками (см. Пустые строки)
 
```coffeescript 
# good -------------------------
App.notify.fileupload
  url      : "#{url}/compile"
  multiple : true
  
  add: (e, data) =>
    file = data.files[0]
  
  yetAnotherFun: (e, data) =>
    # ...
    
# bad --------------------------
App.notify.fileupload
  url           : "#{url}/compile"
  multiple      : true
  add           : (e, data) =>
    file = data.files[0]
  yetAnotherFun : (e, data) =>
    # ...

```

  - если между свойствами присутствует комментарий или пустая строка, это считается "разбиением свойств на группы", и, в этом случае выравнивать свойства нужно отдельно для каждой группы

```coffeescript 
# good -------------------------
obj =
  x : 1
  y : 2
  
  # comment
  fooBar : 3
  option : 4
  
# bad --------------------------
obj =
  x      : 1
  y      : 2
  # comment
  fooBar : 3
  option : 4

```

#### Переменные

  - Для именования переменных необходимо использовать нотацию camelCase
  - Имя переменной должно нормально переводиться на русский язык и использовать правила построения предложений в английском языке
  - Имена приватных переменных, функций и методов должны начинаться с символа подчеркивания _

```coffeescript 
# good -------------------------
for key, value of options
  # ...
  
isConsistent = (originOption, currentOption) -> #... 
  
currentValue = null

# bad --------------------------
for k, v of opts
  # ...
 
cons = (a, b) -> # ...
 
cur = null
```

#### Функции
  - Для именования функций используйте правила именования переменных
  - Название функции должно обозначать действие (отвечать на вопрос "что сделать?")
  - Сохраняйте код функции/метода небольшими. Если размер функции/метода превышает 20 строчек кода, необходимо еще раз подумать о принципах единственной ответственности и сегрегации интерфейсов - скорее всего большой метод можно декомпозировать

```coffeescript 
# good -------------------------
getQuery = -> # ...

addCallbackToObserver = (observer, callback) -> # ...

preventFieldSerialization -> # ...

serializeList = -> # ...
# bad --------------------------
q = -> # ...
query = -> # ...

addToExcludeFieldFromSerialize -> # ...
 
SerializeList = -> # ...
serialize-list = -> # ...
serialize_list = -> # ...
```

  - Запрещается использование однострочной записи функций с использованием терминаторов

```coffeescript 
# good -------------------------
_getElement = (key) ->
  console.log key
  obj[key]

# bad --------------------------
_getElement = (key) -> console.log key; obj[key]

# acceptable -------------------

```

  - Не используйте директиву return в конце функции или метода

```coffeescript 
# good -------------------------
disableButton: ->
  @ui.button.attr "disabled", true

# bad --------------------------
disableButton: ->
  @ui.button.attr "disabled", true
  return

```

  - Однако, если в одной строке функция B используется при вызове функции A, то вызов функции B должен быть описан с использованием скобок.

```coffeescript 
# good -------------------------
preventFieldSerialization getFieldName(el)

# bad --------------------------
preventFieldSerialization getFieldName el
preventFieldSerialization(getFieldName el)

# acceptable -------------------

```

  - Запрещено определять более одной функции в одной строке

```coffeescript 
# good -------------------------
list
   .map (p) -> p.age
   .filter (a) -> a > 20

# bad --------------------------
list.map((p) -> p.age).filter((a) -> a > 20)
```

  - Запрещено описывать в одном выражении вызов нескольких функции, если результат выполнения одной является аргументом вызова другой функции, результат выполнения которой в свою очередь является аргументом для вызова третьей

```coffeescript 
# good -------------------------
date = _formatDate @getRunDate(model), DATE_FORMAT
el.select2 'val', date

# bad --------------------------
el.select2 'val', _formatDate(@getRunDate(model), DATE_FORMAT)
```

  - Используйте разрушение (destructuring) для раскрытия объекта в параметрах функции

```coffeescript 
# good -------------------------
add = ({ a, b }, done) ->
   done null, a + b

# bad --------------------------
add = (params, done) ->
   done null, params.a + params.b
```

 - Толстую стрелку `=>` необходимо использовать только в случаях, когда внутри тела функции или метода необходимо переопределить указатель `this` на родительский объект
 - Для сохранения контекста используйте `=>` вместо описания переменных контекста (aka `_this`, `that`, `self`, etc)
 - Всегда используйте `@` вместо `this`

```coffeescript 
# good -------------------------
getQuery: ->
  _.each @attributes, (attr, key) =>
    if @computed[key] and @errors[key]
      # ...

# bad --------------------------
getQuery: ->
  _model = @
  computed = @computed
  _.each _model.attributes, (attr, key) ->
    if computed[key] and _model.errors[key]
      # ...
```

#### 

```coffeescript 
# good -------------------------


# bad --------------------------
```

#### 

```coffeescript 
# good -------------------------


# bad --------------------------
```

#### 

```coffeescript 
# good -------------------------


# bad --------------------------
```

#### 

```coffeescript 
# good -------------------------


# bad --------------------------
```

#### 

```coffeescript 
# good -------------------------


# bad --------------------------
```

#### 

```coffeescript 
# good -------------------------


# bad --------------------------


# acceptable -------------------

```

#### 

```coffeescript 
# good -------------------------


# bad --------------------------


# acceptable -------------------

```

#### Условия
  - Не допускается обрамление блока условий в скобки
 
```coffeescript 
# good -------------------------


# bad --------------------------
if (
    a is b and
    foo()
)
    bar()

# acceptable -------------------

```
